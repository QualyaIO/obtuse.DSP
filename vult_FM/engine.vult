
// FM with 2 operators, phase modulation

fun process() {
    // audio sampling rate
    mem fs: real;
    // to speedup computations
    mem carrier_half_phase;
    // HACK: using a temp variable force vult to put that in cpp and halves computation times (???)
    val carrier_phase = (modulator:Sinus_wav.process() + 1.0) * carrier_half_phase;

    // modulate the (base) phase of the carrier
    _ = carrier:Sinus_wav.setPhase(carrier_phase);
    val c = carrier:Sinus_wav.process();
    return c;
}
and setSamplerate(newFs:real) {
   if (newFs > 0.0) {
      fs = newFs;
   }
   _ = carrier:Sinus_wav.setSamplerate(fs);
   _ = modulator:Sinus_wav.setSamplerate(fs);
}
// ratio for each operator. Keep in mind the max effective frequency, below samplingrate/2!
and setCarrierRatio(ratio: real) {
   mem carrierRatio = ratio;
}
and setModulatorRatio(ratio: real) {
   mem modulatorRatio = ratio;
}
// effect of modulator [0..1]
and setModulatorLevel(level: real) {
    // directly manipulating the phase amount
    carrier_half_phase = real(carrier:Sinus_wav.getSize()) / 2.0 * level;
}
// should be 0..niquist, in kHz
and setFrequency(freq: real) {
    _ = carrier:Sinus_wav.setFrequency(freq * carrierRatio);
    _ = modulator:Sinus_wav.setFrequency(freq * modulatorRatio);
}
and default() @[init] {
    // default sampling rate (in kHz)
    _ = setSamplerate(44.100);
    // config for operators
    _ = setCarrierRatio(1.0);
    _ = setModulatorRatio(2.0);
    _ = setModulatorLevel(0.1);
    // sarting with LA
    _ = setFrequency(0.440);
}