
// Allpass filter, i.e. feedfowrard + feedback comp filter (in the latter not only delay but also keep last output)
// Output will be kept to -1..1 range

fun process(sample: real) {
    // internal buffer, for delay
    mem buffer;
    // another one to store processed value for next iteration
    mem buffer_allpassed;
    // current position inside it
    mem pos;
    // how far in the buffer in samples we go
    mem delay;
    // attenuation for each pass
    mem decay;
    // multiplicator to keep output amplitude in check (direclty related to decay, computed once)
    mem scale;

    // as per equation
    val out = decay * (sample - buffer_allpassed[pos]) + buffer[pos]; 
    // save to buffers
    buffer[pos] = sample;
    buffer_allpassed[pos] = out;

    // advance and loop on actual delay size    
    pos = pos + 1;
    pos = pos % delay;

    return out * scale;
}

and process_bufferTo(nb: int, input, oBuffer) {
    nb = clip(nb, 0, size(oBuffer));
    if (nb == 0) {
       nb = size(oBuffer);
    }

    // temp variable for filter output
    val out;

    val i = 0;
    while (i < nb) {
       out = decay * (input[i] - buffer_allpassed[pos]) + buffer[pos]; 
       buffer[pos] = input[i];
       buffer_allpassed[pos] = out;
       oBuffer[i] = out * scale;
       pos = pos + 1;
       pos = pos % delay;
       i = i + 1;
    }
}

// setter for decay, here on
and setDecay(newDecay: real) {
  decay = clip(newDecay, 0.0, 1.0);
  scale = 1.0 / (1.0 + 2.0 * decay);
}

// get max delay in sampless
and getMaxDelay() : int {
  return size(buffer);
}

// get max delay in ms
and getMaxDelayms() : real {
  mem fs;
  if (fs <= 0.0) {
     return 0.0;
  }
  // we have frequency in kHz, directly get ms
  return real(size(buffer)) / fs;
}

// change delay (in samples). Will be caped at max buffersize -- check that with getMaxDelay()
// Warning: does *not* reset buffer upon change
// Note: with fixed float cannot above 32767ms (just in case it would make sense to have a 30s buffer on a platform which needs fixed float).,
and setDelay(newDelay: int) {
   mem fs;
   delay = clip(newDelay, 1, getMaxDelay());
   // reset position in buffer
   pos = 0;
}

// change delay (in miliseconds). Will be caped at max buffersize -- check that with getMaxDelayms()
// Note: with fixed float cannot ask above 32767ms (just in case it would make sense to have a 30s buffer on a platform which needs fixed float). Prefer setDelay (in samples) to go beyond
and setDelayms(delayms: real) {
   mem fs;
   setDelay(int(fs * delayms));
}

// seter for parameters. expect values of desired frequency and current sampling rate in kHz to avoid fixed float limits ([1/32767 ..32767]... probably)
// WARNING: will *not* update delay (as it could have been passed with samples directly or by ms), to be called first, and should manually ask to update if delay in ms is important
and setSamplerate(newFs:real) {
   if (newFs > 0.0) {
      fs = newFs;
   }
}

and default() @[init] {
   buffer = Buffer.buffer_large();
   buffer_allpassed = Buffer.buffer_large();
   _ = setSamplerate(44.100);
   _ = setDelayms(50.0);
   _ = setDecay(0.5);
}

// to let vult find above the good buffer size
fun dummy() {
   val buff =  Buffer.buffer();
   _ = process_bufferTo(0, buff, buff);
}
